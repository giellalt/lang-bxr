 !! !!!Buryaad verb affixes

!! This is the file for Buryaad verb morphology. The documentation
!! shortly explains the rationale behind each continuation lexicon.


!! !!The verb classes

!! !Auxiliary verbs

LEXICON aux  !!= * __@CODE@__ is directed to v1 for now.
v1 ; 


!! !Ordinary verbs

LEXICON v1t !!= * @CODE@ the transitive v1 verbs (add +TV and go on)
+V+TV: v1lex ;

LEXICON v1i !!= * @CODE@ the intransitive v1 verbs (add +IV and go on)
+V+TV: v1lex ;

LEXICON v1 !!= * @CODE@ the v1 verbs not yet classified for transitivity
+V: v1lex ;

LEXICON v2
v2lex ;

!! !!Verb morphology
!! We see the contlexes as two layers: The first layer to divide
!! the different stem types into different suffix lexica, and
!! the one for these suffixes.

!! !Layer one: Dividing v1, v2, v3 into suffix lexica

LEXICON v1lex !!= * __@CODE@__ the common v1 morphology
+Ind+Prs:%>н%{A%} persprd ;
+Ind+Prt+Derm:%>б%{A%} persprd ;
+Ind+Prt+Conf:%>л%{A%}й persprd ;
+Imp: imperative ;
Vol: voluntative ;
+Opt: optative ;
+Presc: presc ; ! Prescriptive mood
 participles ;
 converbs ;

LEXICON v2lex !!= * __@CODE@__ the common v2 morphology
+V+Ind+Prs:%>н%{A%} persprd ;
+V+Ind+Prt+Derm:%>б%{A%} past ;
+V+Ind+Prt+Conf:%>л%{A%}й past ;
+V+Imp: imperative ;
+V+Vol: voluntative ;
+V+Opt: optative ;
+V+Presc: presc ; 
+V: participles ;
+V: converbs ;


LEXICON v3 !!= * __@CODE@__ the common v3 morphology
+V+Ind+Prs:%>н%{A%} persprd ;
+V+Ind+Prt+Derm:%>б%{A%} past ;
+V+Ind+Prt+Conf:%>л%{A%}й past ;
+V+Imp: imperative ;
+V+Vol: voluntative ;
+V+Opt: optative ;
+V+Presc: presc ; 
+V: participles ;
+V: converbs ;


!! !Layer two: The suffix lexica themselves.

LEXICON persprd  !!= * __@CODE@__ for suffixes: б, ш, ...
+Sg1:%>б K ;
+Sg2:%>ш  K ;
+Sg3: K ;
+Pl1:%>бди K ;
+Pl2:%>т  K ;
+Pl3: K ;

LEXICON persprd2  !!= * __@CODE@__ for suffixes: би, ши, ...
+Sg1:%>би K ;
+Sg2:%>ши  K ;
+Sg3: K ;
+Pl1:%>бди K ;
+Pl2:%>та  K ;
+Pl3: K ;

LEXICON past   !!= * __@CODE@__ for suffixes: б, 0, ...
+Sg1:%>б K ;
+Sg2:  K ;
+Sg3: K ;
+Pl1:%>б K ;
+Pl2:  K ;
+Pl3: K ;

LEXICON imperative   !!= * __@CODE@__ for imperative suffixes (Sg2, Pl2)
+Sg2:  K ;
+Pl2:%>гты  K ;

LEXICON voluntative   !!= * __@CODE@__ for suffixes: яа, 0, ...
+Sg1:%>яа K ;
+Sg2:  K ;
+Sg3: K ;
+Pl1:%>й%{A%} K ;
+Pl1+Use/NG:%>й%{A%}бди K ;
+Pl2:  K ;
+Pl3: K ;

LEXICON optative   !!= * __@CODE@__ for suffixes: -һууб, ...
+Sg1:%>һ%{U%}%{U%}б K ;
+Sg2:  K ;
+Pl1:%>һ%{U%}%{U%}бди K ;

LEXICON presc  !!= * __@CODE@__ for suffixes: -арайгтыт, ...
+Sg2:%>%{A%}р%{A%}йгтыт K ;
+Pl2:%>%{A%}р%{A%}йгтыш K ;
+Sg2:%>ыт K ;
+Pl2:%>ыш K ;

LEXICON participles   !!= * __@CODE@__ giving 11 different participle tags: +FutPrc, +ImpPrc, ...
+FutPrc:%>х%{A%}   ptc ; 
+ImpfPrc:%>%{A%}%{A%}   ptc ;
+HabPrc:%>д%{A%}г   ptc2 ;
+AgPrc:%>гш%{A%}   ptc ;
+AgConstPrc:%>%{A%}%{A%}ш%{A%}   ptc ;
+PerfPrc:%>һ%{A%}н   ptc2 ;
+PerfPrc+Pl1:%>һ%{A%}мди  # ;
+PotPrc:%>х%{A%}%{A%}р   ptc2 ;
+DualPrc:%>м%{A%}%{A%}р   ptc2 ;
+PassPrc:%>%{A%}%{A%}т%{A%}й   ptc ;
+ResPrc:%>нх%{A%}й   ptc ;

LEXICON ptc   !!= * __@CODE@__ either stop or go to persprd suff
K ;
persprd ;

LEXICON ptc2   !!= * __@CODE@__ either stop or go to persprd2 suff
K ;
persprd2 ;

LEXICON converbs   !!= * __@CODE@__ listing 12 converb suffixes: -н, -жа, ...
+ConMod:%>н    # ;
+ConImpf:%>ж%{A%}    # ;
+ConPrf:%>%{A%}%{A%}д    # ;
+ConCond:%>б%{A%}л    # ;
+ConConc:%>б%{A%}шье    # ;
+ConTerm:%>т%{A%}р    # ;
+ConCntmp:%>мс%{A%}%{A%}р    # ;
+ConAbtmp:%>һ%{A%}%{A%}р    # ;
+ConFin:%>х%{A%}%{A%}р    # ;
+ConIntnt:%>х%{A%}й%{A%}%{A%}    # ; ! хаяа   xajaa
+ConSucc:%>х%{A%}л%{A%}%{A%}р    # ; ! халаар xalaar
+ConCmp:%>нх%{A%}ар    # ;


