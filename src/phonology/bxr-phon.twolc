Alphabet
А Б В Г Д Е Ё Ж З И Й К Л М Н О 
Ө П Р С Т У Ү Ф Х Һ Ц Ч Ш Щ Ь Ы Ъ Э Ю Я
а б в г д е ё ж з и й к л м н о 
ө п р с т у ү ф х һ ц ч ш щ ь ы ъ э ю я
%{A%}:а %{A%}:о %{A%}:э
%{U%}:у %{U%}:ү  
%{Ө%}:а %{Ө%}:о %{Ө%}:э %{Ө%}:ө
%{D%}:т %{D%}:д
%{I%}:и %{I%}:ы %{I%}:0
%{Y%}:и %{Y%}:0  !!= * @CODE@ kept after Cns, deleted after Vow

%> 
а2:а я2:я
%^END:0
 ;

Sets
RealVow =  А Е Ё И О Ө У Ү Ы Э Ю Я
           а е ё и о ө у ү ы э ю я ;
ArchiVow = а2 %{A%} %{U%} %{Ө%} %{I%} %{Y%} ;
RealCns =  Б В Г Д Ж З Й К Л М Н П Р С Т
           Ф Х Һ Ц Ч Ш Щ Ь Ъ
           б в г д ж з й к л м н п р с т
           ф х һ ц ч ш щ ь ъ ;
ArchiCns = %{D%} ;

Vow = RealVow ArchiVow ;
Cns = RealCns ArchiCns ;


Rules

!! !!Vowel harmony
"Vowel harmony A:а"
%{A%}:а <=> [а|я|\ю у]  [RealCns:|ю (у)|ы|и]*  %>: (Cns:) (%{A%}:) _  ;

!@ б а с а г а н >:0 г:0 {A}:а й

"Vowel harmony A:o"
%{A%}:о <=> [о|ё]       [RealCns:|ю (у)|ы|и]*  %>: (Cns:) (%{A%}:) _  ;

"Vowel harmony A:э"
%{A%}:э <=> [э|е|ү|ө ө] [RealCns:|ю (у)|ы|и]*  %>: (Cns:) (%{A%}:) _  ;


"Vowel harmony ӨӨ:аa"
!%{Ө%}:а <=> а (:RealCns) г: %>: һ ( %{Ө%}: ) _  ;
%{Ө%}:а <=> а :RealCns*  %>: һ ( %{Ө%}: ) _  ;
!!€ анг>һ{Ө}{Ө}
!!€ анг>һаа

!!€ аха>һ{Ө}{Ө}
!!€ аха>һаа

"Vowel harmony ӨӨ:оо"
%{Ө%}:о <=> о (:RealCns) %>: г: һ ( %{Ө%}: ) _  ;

"Vowel harmony ӨӨ:ээ"
%{Ө%}:э <=> э (:RealCns) %>: г: һ ( %{Ө%}: ) _  ;

"Vowel harmony ӨӨ:өө"
%{Ө%}:ө <=> ө (:RealCns) %>: г: һ ( %{Ө%}: ) _  ;



!!! ы and и
"{I}{I} to ы in Gen/Acc and Poss 1"
%{I%}:ы <=> [а:0|э:0|о:0] %>: _ %{I%}: [н | е] [%^END:|%>:] ; ! Gen/Acc
            [Cns - н]  %>: _ %{I%}: н ь ;  
                    
"{I}{I} to ы in Gen/Acc and Poss 2"
%{I%}:0 <=> [а:0|э:0|о:0] %>: %{I%}: _ [н | е] [%^END:|%>:] ; ! Gen/Acc
            [Cns - н]  %>: %{I%}: _ н ь ;   
            RealVow %>: %{I%}: _ н ь ;
            RealVow %>: _ %{I%}: н ь ;

!!€ Байгал>{I}{I}нь
!!€ Байгал>ы0нь

!!€ аба>{I}{I}нь
!!€ аба>00нь

"{I}{I} to ии in Gen/Acc and Poss"
%{I%}:и <=>  и: %>:  _      %{I%}: [н | е] [%^END:|%>:] ;
             и: %>:  %{I%}: _      [н | е] [%^END:|%>:] ;
             н  %>:  _      %{I%}: н ь ; ! Poss
             н  %>:  %{I%}: _      н ь ; ! Poss
        RealVow %>: н _      %{I%}: н ь ; ! Poss
        RealVow %>: н %{I%}: _      н ь ; ! Poss

!!€ морин>{I}{I}нь
!!€ морин>иинь

! морин>{I}{I}е>тнай
! мори0>и .. fill in with correct form

"Stem vowel deletion"
Vx:0 <=> _ %>: %{I%}: %{I%}: [%^END:|%>:] ; 
      where Vx in ( и э а о ) ; 

!! !!Other vowel rules
"Final vowel deletion before ые and ын"
Vx:0 <=> :RealCns _ %>: ы [ н | е] ;
     where Vx in ( а о э ) ;

"Deletion SHV"
и:0 <=>  _ н %>: е %{A%}: р ;

"Deletion of ы in word-final ые"
ы:0 <=> [ а й | а а| о о |э э ] %>: _  :е  ;

"Deletion of и in word-final иие"
и:0 <=> и %>: _ :и е ;

"Deletion of Y in predicative particle"
%{Y%}:0 <=> :RealVow %>: б _ ;

!!€ би>б{Y}
!!€ би>б0

"Keep the Y in predicative particle"
%{Y%}:и <=> :RealCns %>: б _ ;

!!€ тэдэнэр>б{Y}
!!€ тэдэнэр>би

!! !!Consonant rules
"Changing t and d"
%{D%}:т <=> [б|г|р|с|д] %>: _ ;

!!€ сэсэг>{D}{A}
!!€ сэсэг>тэ

! Always delete /g/ in affix if there anything apart from /n/

"Трансформация буквы т до д после н и л"  ! check
%{D%}:д <=> [н (г:)|л|й|:RealVow] %>: _ ;

"Deletion of g in word-final ng"
г:0 <=> н _ [%>:  [:Cns - :һ] | %^END:] ;

!!€ анг>{D}{A}
!!€ ан0>да

!!€ анг>һ{Ө}{Ө}
!!€ анг>һаа

"Final consonant voicing in loanwords"
[п:б|к:г|т:д] <=> RealVow _ %>: [Vow|Cns] ;

!!€ самолёт>{A}й
!!€ самолёд>ой

"Deleting the g before g{A}j and gh{Ө}{Ө}"
г:0 <=> Cns: %>: _ [ %{A%}: й | һ %{Ө%}: %{Ө%}: ] ;

!@ б а с а г а н >:0 г:0 {A}:а й
!@ б а с а г а н >:0 г:0 һ {A}:а {A}:а 


"Deletion of possessive m for consonant stems"
м:0 <=> [Cns: - [н:|й:]] %>: _ ;

"Deletion of н before м in possessive"
н:0 <=> RealVow _ %>: м н и ;

"Deletion of n"
н:0 <=> Vx Vx _ %>: г %{A%}: %{A%}: р ;
	where Vx in RealVow ;

!"Буква д после гласных"
! д:д <=> RealVow %>: _ ;


!!€ t e s t w i l l f a i l
!!€ f a i l i t w i l l 0 0 








!"Floating vowel"
!и:ё <=> :Cns _ н %>: () ;



!"{I} to и in Acc case"
!%{I%}:и <=> 




