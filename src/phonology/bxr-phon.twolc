!! !!!Buryaad morphophonology

!! This file expresses Buryaad morphophonological alternation, from abstract underlying forms.

!! !!!Alphabet

!! This is a list of all symbols in use.

Alphabet

А Б В Г Д Е Ё Ж З И Й К Л М Н О   !!= * @CODE@ 
Ө П Р С Т У Ү Ф Х Һ Ц Ч Ш Щ Ь Ы   !!=   @CODE@  
Ъ Э Ю Я							  !!=   @CODE@ = Capital letters
а б в г д е ё ж з и й к л м н о   !!= * @CODE@ 
ө п р с т у ү ф х һ ц ч ш щ ь ы   !!=   @CODE@
ъ э ю я							  !!=   @CODE@ = Small letters
%{A%}:а %{A%}:о %{A%}:э			  !!= * @CODE@ = archivowel A
%{A%}:я %{A%}:ё %{A%}:е			  !!= * @CODE@ = archivowel A
%{U%}:у %{U%}:ү  				  !!= * @CODE@ = archivowel U
%{Ө%}:а %{Ө%}:о %{Ө%}:э %{Ө%}:ө	  !!= * @CODE@ = archivowel Ө 
%{I%}:и %{I%}:ы %{I%}:0			  !!= * @CODE@ = archivowel I
%{Y%}:и %{Y%}:0              !!= * @CODE@ = archivowel Y, kept after Cns, deleted after Vow
%{D%}:т %{D%}:д					  !!= * @CODE@ = arciconsonant D

%> 		        !!= * @CODE@ = suffix border
а2:а я2:я       !!= * @CODE@ = invariant а/я in loanwords
%^END:0	        !!= * @CODE@ = auxiliary end symbol to fix xerox bug
 ;

!! !!!Sets

!! Some of the symbols are bundled together in sets

Sets
RealVow =  А Е Ё И О Ө У Ү Ы Э Ю Я			  !!= * @CODE@ 
           а е ё и о ө у ү ы э ю я ;		  !!=   @CODE@ 
ArchiVow = а2 %{A%} %{U%} %{Ө%} %{I%} %{Y%} ; !!= * @CODE@ 
RealCns =  Б В Г Д Ж З Й К Л М Н П Р С Т	  !!= * @CODE@ 
           Ф Х Һ Ц Ч Ш Щ Ь Ъ				  !!=   @CODE@ 
           б в г д ж з й к л м н п р с т	  !!=   @CODE@ 
           ф х һ ц ч ш щ ь ъ ;				  !!=   @CODE@ 
ArchiCns = %{D%} ;							  !!= * @CODE@ 

Vow = RealVow ArchiVow ;  !!= * @CODE@ 
Cns = RealCns ArchiCns ;  !!= * @CODE@ 
Sgm = Vow Cns ;			  !!= * @CODE@ 


!! !!!Rules

!! Here come all the rules (marked with RULE:), and test cases.

Rules

!! !!Vowel harmony
!! !Vowel harmony for short vowels

"Vowel harmony A:а"
!! * RULE:  __@RULENAME@__ gives a after stems with a or juu
%{A%}:а <=> [:а|:я|\ю у] [RealCns:|ю (у)|ы|а:]* %> [(Cns:* - й:) (%{A%}:)]*      _  ;
            [:а|:я|\ю у] RealCns и:          %>  :я [(Cns:* - й:) (%{A%}:)]* _  ; 
            [:а|:я|\ю у] RealCns и           %> [Cns:+ - й:]  (%{A%}:)               _  ; ! бари>х{A}
            [:а|:я|\ю у] RealCns [и|а]       %> [Cns:+ - й:]  (%{A%}:) й: (%{A%}:) %{A%}: _  ; ! бари>х{A}
            [:а|:я|\ю у] RealCns и           %> [Cns:+ - й:]   %{A%}:  л  (%{A%}:) _  ; ! бари>х{A}

!@ б а с а г а н >:0 г:0 {A}:а й
!!€ басаган>г{A}й
!!€ басаган>0ай

!!€ бари>х{A}л{A}{A}р
!!€ бари>халаар

"Vowel harmony A:o"
!! * RULE:  __@RULENAME@__ gives o after stems with o or jo
%{A%}:о <=> [о|ё]       [RealCns:|ю (у)|ы]*  %> [(Cns:* - й:) (%{A%}:)]* _  ;
            [о|ё]        RealCns и:           %> :ё _  ; 
!!€ самолёт>{D}{A}^END
!!€ самолёд>то0

"Vowel harmony A:э"
!! * RULE:  __@RULENAME@__ gives e after e etc
%{A%}:э <=> [э|е|ү|ө ө] [RealCns:|ю (у)|ы|и]*  %> [(Cns:* - й:) (%{A%}:)]* _  ;
            [э|е|ү|ө ө]  RealCns и:            %> :е _  ;

!@ б а с а г а н >:0 г:0 {A}:а й
!!€ басаган>г{A}й
!!€ басаган>0ай

"Vowel harmony A:я"
!! * RULE:  __@RULENAME@__ gives я after stems with a or juu
%{A%}:я <=> [:а|:я|\ю у]  [RealCns:|ю (у)|ы|и|:0]* %>  й:            _ ;
            [:а|:я|\ю у]   RealCns          и:     %>                _ ;
            [:а|:я|\ю у]   RealCns      [а|и]      %> :Cns %{A%}: й: _ ;

!!€ хара>х{A}й{A}{A}
!!€ хара>ха0яа
!!€ бари>х{A}й{A}{A}
!!€ бари>ха0яа

"Vowel harmony A:ё"
!! * RULE:  __@RULENAME@__ gives ё after stems with o or jo
%{A%}:ё <=> [о|ё]       [RealCns:|ю (у)|ы|и]*  %> й: _  ;

!!€ самолёт>{D}{A}^END
!!€ самолёд>то0

"Vowel harmony A:е"
!! * RULE:  __@RULENAME@__ gives е after e etc and preceeding 
%{A%}:е <=> [э|е|ү|ө ө] [RealCns:|ю (у)|ы|и]*  %> й: _  ;

"Vowel harmony й:0"
!! * RULE:  __@RULENAME@__ deletes й in front of A
й:0 <=>  %> RealCns:* (%{A%}:) _ %{A%}: ;

"Vowel harmony и:0"
!! * RULE:  __@RULENAME@__ deletes и in front of A initial suffix
и:0 <=> _ %> %{A%}: ;

! a бариха+V+ImpfPrc  = lexc upper:lower a:b
! b бари>{A}{A}
! b бари>{A}{A}       = twolc upper:lower b:c
! c бар0яа

! a   b    a = бариха+V+ImpfPrc
! - x - =  -
! b   c    c = баряа
! = a x b / b x c = a x c 
 

"Vowel harmony U:у"
!! * RULE:  __@RULENAME@__ gives a after stems with a or juu
%{U%}:у <=> [:а|:я|\ю у]  [RealCns:|ю (у)|ы|и|:0]*  %> (Cns:) (%{U%}:) _  ;


"Vowel harmony U:ү"
!! * RULE:  __@RULENAME@__ gives e after e etc
%{U%}:ү <=> [э|е|ү|ө ө] [RealCns:|ю (у)|ы|и]*  %> (Cns:) (%{U%}:) _  ;



!! !Vowel harmony for long vowels

"Vowel harmony ӨӨ:аa"
!! * RULE:  __@RULENAME@__ gives aa after a
!%{Ө%}:а <=> а (:RealCns) г: %> һ ( %{Ө%}: ) _  ;
%{Ө%}:а <=> а :RealCns*  %> ([:RealCns|:RealVow]*  %>:) һ ( %{Ө%}: ) _  ;

!!€ анг>һ{Ө}{Ө}^END
!!€ анг>һаа0

!!€ аха>һ{Ө}{Ө}^END
!!€ аха>һаа0

"Vowel harmony ӨӨ:оо"
!! * RULE:  __@RULENAME@__ gives oo after o
%{Ө%}:о <=> о (:RealCns) %> г: һ ( %{Ө%}: ) _  ;

"Vowel harmony ӨӨ:ээ"
!! * RULE:  __@RULENAME@__ gives ee after e
%{Ө%}:э <=> э (:RealCns) %> г: һ ( %{Ө%}: ) _  ;

"Vowel harmony ӨӨ:өө"
!! * RULE:  __@RULENAME@__ gives ø after ø
%{Ө%}:ө <=> ө (:RealCns) %> г: һ ( %{Ө%}: ) _  ;



!! !Realisation of I as ы and и

"{I}{I} to ы in Gen/Acc and Poss 1"
!! * RULE:  __@RULENAME@__ gives y for the first I
%{I%}:ы <=> [а:|э:|о:|:RealCns] %> _ %{I%}: [н|е] [%^END:|%>:] ; !Gen/Acc
                          н:    %> _ %{I%}:     е [%^END:|%>:] ; !Gen/Acc
                     [Cns - н]  %> _ %{I%}: н ь ;  
                    
"{I}{I} to ы in Gen/Acc and Poss 2"
!! * RULE:  __@RULENAME@__ deletes the second I
%{I%}:0 <=> [а:|э:|о:|:RealCns] %> %{I%}: _ [н|е] [%^END:|%>:] ; !Gen/Acc
                          н:    %> %{I%}: _        е [%^END:|%>:] ; !Gen/Acc
!                          н:    %>        _ %{I%}: е [%^END:|%>:] ; !Gen/Acc
                     [Cns - н]  %> %{I%}: _ н ь ;   
                     RealVow    %> %{I%}: _ н ь ;
                     RealVow    %>        _ %{I%}: н ь ;


!!€ Байгал>{I}{I}нь^END
!!€ Байгал>ы0нь0

!!€ аба>{I}{I}нь^END
!!€ аба>00нь0

!!€ аба>{I}{I}е^END
!!€ аб0>ы0е0

"{I}{I} to ии in Gen/Acc and Poss"
!! * RULE:  __@RULENAME@__ changes both I to i
%{I%}:и <=>  и: %>          _ %{I%}: [н | е] [%^END:|%>:] ;
             и: %>   %{I%}: _        [н | е] [%^END:|%>:] ;
             н  %>          _ %{I%}: н ь ; ! Poss
             н  %>   %{I%}: _        н ь ; ! Poss
        RealVow %> н        _ %{I%}: н ь ; ! Poss
        RealVow %> н %{I%}: _        н ь ; ! Poss

!!€ морин>{I}{I}нь^END
!!€ морин>иинь0

! морин>{I}{I}е>тнай
! мори0>и .. fill in with correct form


!"Stem vowel deletion"
!Vx:0 <=> _ %> :а :а ; ![%^END:|%>:] ; 
!      where Vx in ( и э а о ) ; 

!! !!Other vowel rules
!! !Vowel deletion rules

"Final stem vowel deletion before ые and ын"
!! * RULE:  __@RULENAME@__ 
Vx:0 <=> :RealCns _ %> [:ы (:0) [н|е]|:а :а|:о :о|:э :э|:ө :ө] ;
!         :RealCns _ %> :RealVow ;
     where Vx in ( а о э ) ;

!!€ аха>{A}{A}р^END
!!€ ах0>аар0

"Deletion of и in word-final иие and Deletion SHV"
!! * RULE:  __@RULENAME@__ 
и:0 <=>       _ н %> е %{A%}: р ;
        и %> _ :и е ;


"Deletion of ы in -ые for aj and VV stems"
!! * RULE:  __@RULENAME@__ 
ы:0 <=> [ а й | а а| о о |э э ] %> _  :е  ;

"Deletion of Y in predicative particle -b for vowel stem"
!! * RULE:  __@RULENAME@__ 
%{Y%}:0 <=> :RealVow %> б _ ;

!!€ би>б{Y}^END
!!€ би>б00

!! !Vowel change rules

"Y to i in predicative particle -bi for consonant stem"
!! * RULE:  __@RULENAME@__ 
%{Y%}:и <=> :RealCns %> б _ ;

!!€ тэдэнэр>б{Y}^END
!!€ тэдэнэр>би0

!! !!Consonant rules
!! !Consonant change rules

"D to t suffix initially for VC stems"
!! * RULE:  __@RULENAME@__ 
%{D%}:т <=> :RealVow [б|г|р|с|:д] %> _ ;
!%{D%}:т <=> [б|г|р|с|д|т] %> _ ;

!!€ сэсэг>{D}{A}^END
!!€ сэсэг>тэ0

"D to d after n i l"  ! check
!! * RULE:  __@RULENAME@__ 
%{D%}:д <=> [н (г:)|л|й|:RealVow] %> _ ;



"Final consonant ptd voicing to bdg in loanwords"
!! * RULE:  __@RULENAME@__ 
[п:б|т:д|к:г] <=> RealVow _ %> [Vow|:Cns] ;

!!€ самолёт>{A}й^END
!!€ самолёд>ой0


!! !Consonant deletion rules

"Deletion of possessive m for consonant stems"
!! * RULE:  __@RULENAME@__ 
м:0 <=> [Cns: - [н:|й:]] %> _ ;

"Deletion of n before m in possessive and before gAAr"
!! * RULE:  __@RULENAME@__ 
н:0 <=>         RealVow _ %> м н и ;
        RealVow RealVow _ %> г %{A%}: %{A%}: р ;
        RealVow RealVow _ %> %{I%}: %{I%}: е ;

!!€ һанаан>{I}{I}е^END
!!€ һанаа0>ы0е0

!"Буква д после гласных"
! д:д <=> RealVow %> _ ;


! Always delete /g/ in affix if there anything apart from /n/

"Deletion of g in word-final ng and before gaj and haa"
!! * RULE:  __@RULENAME@__ 
г:0 <=>        н _ [%>:  [:Cns - :һ] | %^END:] ;
        Cns: %> _ [ %{A%}: [%{A%}: р|й] | һ %{Ө%}: %{Ө%}: ] ;

!@ б а с а г а н >:> г:0 {A}:а й
!@ б а с а г а н >:> г:0 һ {A}:а {A}:а 

!!€ анг>{D}{A}^END
!!€ ан0>да0

!!€ анг>һ{Ө}{Ө}^END 
!!€ анг>һаа0




! This magic spell in order to show make check output also when everything passes:
!€ т е с т р а б о т а е т
!€ в т е с т е о ш и б к а








!"Floating vowel"
!и:ё <=> :Cns _ н %> () ;



!"{I} to и in Acc case"
!%{I%}:и <=> 




