!! !!!Buryaad noun affixes

!! We have two lexica, n1 and n2, 
!! n1 with -нар plural and V-initial Gen, Ins, and
!! n2 with -ууд plural and г-initial Gen, Ins

!! !! The noun classes

LEXICON nx !!= * __@CODE@__ unclassified nouns, to n2 for now
n2 ;

LEXICON n1 !!= * __@CODE@__ with Gen -IIN, and -нар- suffix for oblique cases
  +N+Sg:      case1 ;
  +N+Pl:%>нар case1 ; ! was uncommented

LEXICON n2 !!= * __@CODE@__ with Gen -GAj and -ууд suffič for oblique cases
  +N+Sg:      case2 ;
  +N+Pl:%>ууд case2 ; ! was uncommented

!! !! The class-specific case morphology (Gen, Ins)
LEXICON case1 !!= * __@CODE@__ Gen, Ins for n1 nouns, and pointing to common cases
  +Gen:%>%{I%}%{I%}н K    ;
  +Ins:%>%{A%}%{A%}р K    ;
  commoncase ;

LEXICON case2 !!= * __@CODE@__ Gen, Ins for n2 nouns, and pointing to common cases
  +Gen:%>г%{A%}й K ;
  +Ins:%>г%{A%}%{A%}р K    ;
  commoncase ;

LEXICON case3 !!= * __@CODE@__ case3 are numerals, for now pointing to case2
case2 ; !

!! !! The morphology common to the different stem classes

LEXICON commoncase  !!= * __@CODE@__ common treatment of Nom, Dat, Acc, Abl, Com for all nouns
  +Nom:               Poss ;
  +Dat:%>%{D%}%{A%}   K    ;
  +Acc:%>%{I%}%{I%}е  Poss ;
  +Abl:%>һ%{Ө%}%{Ө%}  K    ;
  +Com:%>т%{A%}й      K    ;

LEXICON Poss !!= * __@CODE@__ for Px, common for all nouns
  K ; ! Absolute declension
+PxSg1:%>мни K ; ! final C, V
+PxSg1:%>м2ни K ; ! final C
+PxSg1+Use/NG:%>м K ; ! in case of one, prefer -мни
+PxSg2:%>шни K ;
+PxSg2+Use/NG:%>ш K ;
+PxPl1:%>мнай K ;
+PxPl2:%>тнай K ;
+Px3:%>%{I%}%{I%}нь K ;
+Px3:%>н%{I%}%{I%}нь K ; ! keep the n in mind
